set encoding=utf8
using modex Organization-modex.xml
using modex Location-modex.xml
using groups Organization
using groups Location
set defaultAction=>CreateSpecificEntity()

# .com companies
com:$NP .::ORGANIZATION:
=>CreateSpecificEntity()

# list of organizations common to all languages
include ../../common/companies.rules

# a list of known organizations
include organization.rules
include organizationkey.lst

# newspapers and television
include newspapersAndMedia.rules


use ../Location/cities.lst,../Location/states.lst,../Location/countriesAndRegions.lst

# location based organization
@locationOrg=(
University,
International University,
Supreme Court,
Circuit Court,
Prison,
Police,
Police Department
)

@conjunctions=(
of,
the,
and,
\&)

use organizationkey.lst
@organizationKey:(t_capital_1st|t_capital|t_capital_small|\&){1-5}:(.)?:ORGANIZATION:
@organizationKey:(t_capital_1st|t_capital){1-3} @conjunctions{0-2} (t_capital_1st|t_capital){0-3}:(.)?:ORGANIZATION:

t_capital::\( t_capital_1st{1-3} @conjunctions{1-2} t_capital_1st{1-3} \):ORGANIZATION:

# [GC 20130906] commented out because this rule matches wrong things and remove tokens...
#t_capital_1st::[\(] t_capital_1st{1-3} @conjunctions{1-2} t_capital_1st{1-3} [\)]:ORGANIZATION:


t_capital_1st::t_capital_1st{1-3} [\(] <Location.LOCATION>] [\)]:NOT_ORGANIZATION:
[<Location.LOCATION>]::[\(] [t_capital_1st{1-3}] [\)]:ORGANIZATION:
[The]::t_capital_1st{1-3} [\(] [t_capital] [\)]:ORGANIZATION:
t_capital_1st::t_capital_1st{1-3} ['] t_capital_1st? [\(] [t_capital] [\)]:ORGANIZATION:
t_capital_1st::t_capital_1st{1-3} [\(] [t_capital] [\)]:ORGANIZATION:
t_capital_1st::t_capital_1st{1-3} @conjunctions{1-2} t_capital_1st{1-2} [\(] [t_capital] [\)]:ORGANIZATION:
[<Organization.ORGANIZATION>]::[\(] t_capital [\)]:ORGANIZATION:
t_capital::[\(] [t_capital_1st{1-3}] [\)]:ORGANIZATION:
[<Organization.ORGANIZATION>]::[\(] t_capital_1st{1-3} [\)]:ORGANIZATION:
t_capital_1st:[\(] [<Organization.ORGANIZATION>] [\)]:t_capital_1st{1-3}:ORGANIZATION:
t_capital_1st:[(The|the)]:t_capital_1st{0-2} [said]:ORGANIZATION:
# ORGANIZATION:::ORGANIZATION:

University:(State)?:of <LOCATION> School of (t_capital_1st|t_capital|t_capital_small):ORGANIZATION:
University:(State)?:of <LOCATION>:ORGANIZATION:

Association:(National)?:of (t_capital_1st|t_capital|t_capital_small|\&){1-5}:ORGANIZATION:

@City::(@locationOrg):ORGANIZATION:
@locationOrg::of? (@City|@State):ORGANIZATION:
@State::state? (@locationOrg):ORGANIZATION:
@locationOrg:::ORGANIZATION:

use organizationType.lst

university::of (@City|@State):ORGANIZATION:
#university::of (@City|@State) (@City|@State)?:ORGANIZATION:
@organizationType::(of|de) (t_capital|t_capital_1st){1-3}:ORGANIZATION:
F.C.::(t_capital|t_capital_1st){1-2}:ORGANIZATION:
#------------------------------
# a list of known organizations
use newspaperKey.lst,organization.lst,../../common/organizationCommon.lst
use AutomobileManu.lst
# use companies.lst


# @companies:::ORGANIZATION:
@Autos:::ORGANIZATION:

# abbreviated newspaper names
@newspaperPrincipalKey:::ORGANIZATION:

@organization:::ORGANIZATION:
@organizationCommon:::ORGANIZATION:
